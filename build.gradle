apply plugin: "war"

// Set name of the generated war file
war.archiveName = "webapp.war"

version = "0.1"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url "http://repo1.maven.org/maven2" }
}

dependencies {
    // External compile time dependencies
    compile(
        [group: "javax", name: "javaee-api", version: "7.0"], // Java EE classes
        [group: "commons-io", name: "commons-io", version: "2.5"],
        [group: "org.apache.httpcomponents", name: "httpcore", version: "4.4.1"],
        [group: "org.apache.httpcomponents", name: "httpclient", version: "4.4.1"],
        [group: "log4j", name: "log4j", version: "1.2.17"],
        [group: "org.quartz-scheduler", name: "quartz", version: "2.2.3"],
        [group: "com.google.code.gson", name: "gson", version: "2.3.1"],
        [group: "org.postgresql", name: "postgresql", version: "9.4.1210.jre7"],
        [group: "org.apache.commons", name: "commons-dbcp2", version: "2.1.1"]
    )
    
    // Local compile time dependencies.
    compile fileTree(dir: "lib", include: "**/*.jar")
}

task deploy(type: Copy, dependsOn: ["build"]) {
    from war.archivePath
    into "${tomcat_home}/webapps"
}

task undeploy(type: Delete) {
    delete "${tomcat_home}/webapps/${war.archiveName}"
}

task start_tomcat(type: Exec) {
    workingDir "${tomcat_home}/bin"
    commandLine "./startup.sh"
}

task stop_tomcat(type: Exec) {
    workingDir "${tomcat_home}/bin"
    commandLine "./shutdown.sh"
}